module NeuralHelpersTests

using Test
using Gridap
using GridapROMs
using GridapROMs.RBSteady
using NonlinearROMs
using Lux
using Optimisers

@testset "Formatting and Utils" begin
  # format_eta
  @test NonlinearROMs.format_eta(45) == "00:45"
  @test NonlinearROMs.format_eta(125) == "02:05"
  @test NonlinearROMs.format_eta(3665) == "01:01:05"

  # batch_size resolution
  @test resolve_batch_size(0,100) == 100
  @test resolve_batch_size(-5,100) == 100
  @test resolve_batch_size(32,100) == 32
end

@testset "Z-Score stats computation" begin
  data = Float32[1 2 3;4 5 6] # 2 features,3 samples
  stats = ZScore(data)

  @test size(stats.μ) == (2,)
  @test size(stats.σ) == (2,)
  @test stats.μ[1] ≈ 2.0f0

  # Array of ones => dev = 0 converted to 1
  data_const = ones(Float32,2,10)
  stats_const = ZScore(data_const)
  @test all(stats_const.μ .≈ 1.0f0)
  @test all(stats_const.σ .== 1.0f0) # Forced to 1.0
end

@testset "Learning Rate Schedulers" begin
  # CosineAnnealing
  ca = CosineAnnealing(100,lr_max=1.0f0,lr_min=0.0f0)
  @test get_lr(ca) == 1.0f0

  opt_state = Optimisers.setup(Adam(1.0f0),[1.0f0])
  # Half training (50/100),cos(pi/2) = 0,lr = 0.5
  step_scheduler!(ca,opt_state,50,1.0f0)
  @test opt_state.rule.eta ≈ 0.5f0

  # ReduceLROnPlateau
  plat = ReduceLROnPlateau(patience=2,factor=0.5f0,min_lr=0.1f0,start_lr=1.0f0)
  @test get_lr(plat) == 1.0f0

  opt_state_plat = Optimisers.setup(Adam(1.0f0),[1.0f0])

  # Epoch 1: improvement
  step_scheduler!(plat,opt_state_plat,1,0.5f0)
  @test plat.wait[] == 0

  # Epoch 2: No improvement
  step_scheduler!(plat,opt_state_plat,2,0.6f0)
  @test plat.wait[] == 1
  @test opt_state_plat.rule.eta == 1.0f0 # No drop yet

  # Epoch 3: Patience limit reached,drop lr by half
  step_scheduler!(plat,opt_state_plat,3,0.6f0)
  @test opt_state_plat.rule.eta ≈ 0.5f0
  @test plat.wait[] == 0 # Patience resetted
end

@testset "Model Resolution and Arch Building" begin
  # DeepONet: auto-sized from input dims
  model_don = DeepONet(5,2;width=32,depth=2)
  @test model_don.branch_layers == (5,32,32,32)
  @test model_don.trunk_layers == (2,32,32,32)

  # NOMAD: auto-sized from input dims
  model_nomad = NOMAD(10,3;width=64,depth=3)
  @test model_nomad.approximator_layers == (10,64,64,64,64)
  @test model_nomad.decoder_layers == (67,64,64,64,1)

  # Explicit layer construction
  explicit_don = DeepONet(branch_layers=(5,10),trunk_layers=(2,10),activation=tanh)
  @test explicit_don.branch_layers == (5,10)
  @test explicit_don.trunk_layers == (2,10)

  explicit_nomad = NOMAD(approximator_layers=(10,10),decoder_layers=(13,1),activation=tanh)
  @test explicit_nomad.approximator_layers == (10,10)
  @test explicit_nomad.decoder_layers == (13,1)

  # build_lux_chain
  chain = NonlinearROMs.build_lux_chain((2,10,10,1),tanh)
  @test chain isa Lux.Chain
  @test length(chain.layers) == 3

  @test chain.layers[1].out_dims == 10
  @test chain.layers[2].out_dims == 10
  @test chain.layers[3].out_dims == 1
  @test chain.layers[1].activation === tanh
  @test chain.layers[2].activation === tanh
  @test chain.layers[3].activation === identity
end

@testset "Explicit Lux Constructors" begin
  # LuxDeepONet and LuxNOMAD
  branch_net = Lux.Dense(5 => 10)
  trunk_net = Lux.Dense(2 => 10)
  lux_don = NonlinearROMs.LuxDeepONet(branch_net,trunk_net)

  @test lux_don isa Lux.Chain
  @test hasproperty(lux_don.layers,:layer_1)
  @test lux_don.layers.layer_1 isa Lux.Parallel
  @test lux_don.layers.layer_1.connection == *

  approx_net = Lux.Dense(10 => 10)
  dec_net = Lux.Dense(13 => 1)
  lux_nomad = NonlinearROMs.LuxNOMAD(approx_net,dec_net)

  @test lux_nomad isa Lux.Chain
  @test lux_nomad.layers.layer_1 isa Lux.Parallel
  @test lux_nomad.layers.layer_1.connection == vcat
  @test lux_nomad.layers.layer_2 === dec_net
end

@testset "Coordinate Extraction" begin
  # Small 1D mesh: domain (0,1) with 2 elements -> Nodes: 0.0,0.25,0.5,0.75,1.0
  model = CartesianDiscreteModel((0.0,1.0),(2,))
  reffe = ReferenceFE(lagrangian,Float64,2)
  V = LexicographicFESpace(model,reffe)
  @test get_free_dof_coordinates(V) == Point.([0.0,0.25,0.5,0.75,1.0])
end

# Dummy Scheduler to test interface fallback
struct DummyScheduler <: NonlinearROMs.LRScheduler end

@testset "LRScheduler Interface Fallbacks" begin
  dummy = DummyScheduler()

  # Should throw ErrorException if methods are not implemented
  @test_throws ErrorException get_lr(dummy)
  @test_throws ErrorException step_scheduler!(dummy,nothing,1,10,0.5f0)
end

end # module
